<html>

<head>
<title>f_castel</title>
<meta http-equiv="content-type" content="text/html; charset=UTF-8">
<link rel="stylesheet" href="styles.css">
<script type="text/javascript" src="glMatrix-0.9.5.min.js"></script>

<script id="shader-fs" type="x-shader/x-fragment">
  precision mediump float;
  precision mediump int;

  const bool PERF_DEBUG = false;
  const float NUMBER_OF_SCENES = 3.0;
  const float MAX_DISTANCE = 170.0;
  const float IMAGE_WIDTH = 400.0;
  const float EPSILON = 0.2;
  const float PI = 3.14159265359;
  const int MAX_ITERATIONS = 100; // ints on mobile go span -2^16, 2^16
  const vec3 light1 = vec3(-300.0, 200.0, -1035.0);
  const vec3 light2 = vec3(-600.0, -200.0, -400.0);
  vec3 baseMaterial = vec3(0.92);
  const vec3 specMaterial = vec3(1.0);
  const vec3 ambientMaterial = vec3(0.04);
  const float shininess = 8000.0;
  const int RAYS_PER_PIXEL = 10;    // total number of rays cast in full AA scenario (edges)
  const int MISS_THRESHOLD = 2;     // number of surface misses before killing the loop
  const int THRESHOLD_TEST = 3;     // number of samples to take for col threshold test
  const float COL_THRESHOLD = 0.70; // if the deltas between min and max vals for r+g+b channels
                                    // exceed this value, we've detected an edge!

  float fov = 50.0;
  float fovx = PI * fov / 360.0;
  float fovy = fovx * 1.0; // height/width = 1.0 in the square canvas
  float ulen = tan(fovx);
  float vlen = tan(fovy);
  const vec3 camPos = vec3(0.0, 0.0, -120.0);
  const vec3 camDir = vec3(0.0, 0.0, 1.0);
  const vec3 camUp = vec3(0.0, 1.0, 0.0);

  float camOrbitSpeed = 20.0;

  float sDistCalls = 0.0;
  float shortestDistance = 100.0;
  bool surfaceHit = false; // keeps track of whether or not we've hit the surface at least once

  // Inputs
  varying vec2 uv;
  uniform float t; // time as frame number
  uniform float currentScene; // one scene per int, interpolate in-between
  uniform sampler2D u_image; // reflection texture

  // Utils
  float max(float a, float b, float c){
    return max(a, max(b, c));
  }

  float max(float a, float b, float c, float d){
    return max(max(a, b), max(c, d));
  }

  float min(float a, float b, float c){
    return min(min(a, b), c);
  }

  float min(float a, float b, float c, float d){
    return min(min(a, b), min(c, d));
  }

  mat4 rotationMatrix(vec3 axis, float angle){
      axis = normalize(axis);
      float s = sin(angle);
      float c = cos(angle);
      float oc = 1.0 - c;
      
      return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
                  oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
                  oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
                  0.0,                                0.0,                                0.0,                                1.0);
  }

  vec3 rotate(vec3 vec, mat4 rmat){
    vec4 vf = vec4(vec, 0.0);
    vf = rmat * vf;
    return vec3(vf[0], vf[1], vf[2]);
  }

  float snoise(in vec2 co){
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
  }


  float intersectReflectionPlane(vec3 p, vec3 ray){
    // dot the plane normal with the ray direction
    float denom = dot(ray, vec3(1.0, 0.0, 0.0));
    if(abs(denom) > EPSILON){
        float t = (-dot(p, vec3(1.0, 0.0, 0.0)) - 65.0) / denom;
        return t;
    }
    return -1.0;
  }

  // Primitive distance functions
  float sphere(vec3 p, vec3 orig, float r){
    return length(p-orig) - r;
  }

  float baseOrigCube(vec3 p, float hdim){
    return max(abs(p[0]) - hdim, abs(p[1]) - hdim, abs(p[2]) - hdim);
  }

  float cube(vec3 p, vec3 orig, float dim){
    float hdim = dim/2.0;
    vec3 modP = abs(p - orig);
    if(modP[0] > hdim || modP[1] > hdim || modP[2] > hdim){
      vec3 sph = vec3(min(modP[0], hdim), min(modP[1], hdim), min(modP[2], hdim));
      return length(modP - sph);
    } else {
      return baseOrigCube(modP, hdim);
    }
  }

  float torus(vec3 p, vec3 orig, float drad, float trad){
    vec2 q = vec2(length(p.xz) - drad, p[1]);
    return length(q) - trad;
  }

  // Combination operators
  float cSmooth(float a, float b, float r){
    float res = exp(-r*a) + exp(-r*b);
    return -log(res)/r;
  }

  float cSmooth(float a, float b, float c, float d, float r){
    return cSmooth(cSmooth(a, b, r), cSmooth(c, d, r), r);
  }

  float cBool(float a, float b){
    return max(a, b);
  }

  // Scene distance functions
  float sDist2(vec3 p){
    float base = sphere(p, vec3(0.0, 0.0, 0.0), 25.0 + 5.0*sin(0.007*t+0.35));
    float s1 = sphere(p, vec3(7.0*cos(0.05*t)+11.3, -9.0, 4.0*sin(0.7+0.08*t)+4.0), 19.0);
    float s2 = sphere(p, vec3(12.0*sin(0.018*t)+11.3, 14.0, 4.0*sin(0.2+0.032*t)+4.0), 15.0);
    float s3 = sphere(p, vec3(10.0*sin(0.06-0.02*t)+16.0, 14.0*cos(0.013*t)-4.0, 4.0), 20.0);
    float s4 = sphere(p, vec3(10.0, 14.0*cos(0.013*t)-4.0, -10.0*sin(0.03*t)+5.0), 22.0);
    
    p = -p;
    float s5 = sphere(p, vec3(7.0*cos(0.09*t)+11.3, -9.0, 4.0*sin(0.7+0.08*t)+4.0), 19.0);
    float s6 = sphere(p, vec3(12.0*sin(0.43-0.018*t)+11.3, 9.0, 4.0*sin(0.8+0.032*t)+4.0), 4.0*cos(0.008*t)+21.0);
    float s7 = sphere(p, vec3(10.0*sin(0.02*t)+16.0, 14.0*cos(0.3-0.013*t)-4.0, 4.0), 20.0);
    float s8 = sphere(p, vec3(10.0, 9.0*cos(0.4+0.013*t)+4.0, -10.0*sin(0.06*t)-6.0), 15.0);
    
    float g1 = cSmooth(s1, s2, 0.2);
    float g2 = cSmooth(s3, s4, 0.2);
    float g3 = cSmooth(s5, s6, 0.2);
    float g4 = cSmooth(s7, s8, 0.2);
    float group = cSmooth(g1, g2, g3, g4, 0.3);
    
    float blob = cSmooth(base, group, 0.5);
    return blob;
  }

  float sDist1(vec3 p){
    p = 0.8 * p;
    float nt = 0.01*t;
    vec4 np1 = vec4(p, 0.0);
    vec4 np2 = np1;
    vec4 np3 = np1;
    vec4 np4 = np1;
    np1 = np1 * rotationMatrix(vec3(0.0, 1.0, 0.0), -nt);
    np2 = np1 * rotationMatrix(vec3(1.0, 0.0, 0.0), -nt*0.9);
    np3 = np1 * rotationMatrix(vec3(0.0, 0.4, 1.0), nt*1.24);
    np4 = np4 * rotationMatrix(vec3(-0.6, 0.7, 0.1), nt*1.67);
    return max(cube(np3.xyz, vec3(0.0, 0.0, 0.0), 40.0), 
      min(cube(np1.xyz, vec3(0.0, 0.0, 0.0), 40.0),
        cube(np2.xyz, vec3(0.0, 0.0, 0.0), 40.0),
        cube(np4.xyz, vec3(0.0, 0.0, 0.0), 36.0)));
  }

  float sDist3(vec3 p){
    float nt = 0.03*t;
    vec4 p1 = vec4(p, 0.0) * rotationMatrix(vec3(1.0, 1.0, 0.0), 0.3) * rotationMatrix(vec3(0.0,0.0,1.0), nt);
    vec4 p2 = vec4(p, 0.0) * rotationMatrix(vec3(0.3, 1.0, 0.6), 0.8) * rotationMatrix(vec3(1.0,0.0,0.0), 0.7*nt);
    vec4 p3 = vec4(p, 0.0) * rotationMatrix(vec3(0.3, 0.0, 0.6), 0.8) * rotationMatrix(vec3(1.0,0.0,0.0), -0.6*nt);
    vec4 p4 = vec4(p, 0.0) * rotationMatrix(vec3(1.3, 0.9, 0.6), -0.8) * rotationMatrix(vec3(1.0,0.0,1.0), 0.6*nt);
    return min(
      min(torus(p1.xyz, vec3(0.0), 30.0, 1.0),
        torus(p2.xyz, vec3(0.0), 35.0, 1.0),
        torus(p3.xyz, vec3(0.0), 25.0, 1.0),
        torus(p4.xyz, vec3(0.0), 32.5, 1.0)),
      sphere(p, vec3(0.0), 20.0));
  }

  float sDist(vec3 p){
    sDistCalls += 1.0;

    float cscene = mod(currentScene, NUMBER_OF_SCENES);
    if(cscene == 0.0)
      return sDist1(p);
    else if(cscene <= 1.0)
      return sDist2(p);
    else
      return sDist3(p);
  }
  
  // 6 point sample normal
  vec3 sNormal(vec3 p){
    vec3 a = p + EPSILON*vec3(-1,  0,  0);
    vec3 b = p + EPSILON*vec3( 1,  0,  0);
    vec3 c = p + EPSILON*vec3( 0, -1,  0);
    vec3 d = p + EPSILON*vec3( 0,  1,  0);
    vec3 e = p + EPSILON*vec3( 0,  0, -1);
    vec3 f = p + EPSILON*vec3( 0,  0,  1);
    return
      normalize(
        sDist(a)*(a-p) +
        sDist(b)*(b-p) +
        sDist(c)*(c-p) +
        sDist(d)*(d-p) +
        sDist(e)*(e-p) +
        sDist(f)*(f-p));
  }

  // Blinn-Phong shader
  vec3 blinnPhongShade(vec3 p, vec3 eye, vec3 light){
    vec3 n = sNormal(p);
    vec3 eyeToPoint = normalize(p - eye);
    vec3 pointToLight = normalize(light - p);
    if(dot(n, eyeToPoint) > 0.0) return ambientMaterial;
    else {
      float sdn = max(dot(pointToLight, n), 0.0);
      vec3 diffuse = baseMaterial * sdn;
      float cosAngInsicende = dot(n, pointToLight);
      vec3 halfAngle = light - p + eye - p;
      float halfAngleLen = length(halfAngle);
      halfAngle = normalize(normalize(halfAngle) * 1.0/halfAngleLen);
      float blinnTerm = dot(halfAngle, n);
      if(blinnTerm < 0.0 || cosAngInsicende == 0.0) blinnTerm = 0.0;
      else if(blinnTerm > 1.0) blinnTerm = 1.0;
      return ambientMaterial + diffuse + specMaterial*pow(blinnTerm, shininess);
    }
  }

  vec3 blinnPhongShade(vec3 p, vec3 eye){
    return blinnPhongShade(p, eye, light1);
  }

  // scene texture function
  vec3 sTexture(vec3 p){
    const float range_max = 0.92;
    float d = length(p + vec3(13.0*sin(0.01*t), -14.0*cos(0.009*t), sin(0.006*t)*8.0));
    const float dRange = 28.0;
    float range_min = 0.92 + (sin(0.02*t) -1.0)*0.1;
    
    float range_span = range_max - range_min;
    float x = mod(d, dRange) / dRange;
    vec3 rainbow = vec3(0.0, 0.0, 0.0);
    for(int i = 0; i < 3; i++){
      rainbow[i] = mix(range_span, 0.0, min(x*3.0, (1.0-x)*3.0, 1.0));
      x = mod(x + 0.3333333, 1.0);
    }
    return rainbow+vec3(range_min);
  }

  vec4 traceRay(vec3 pixel, vec3 ray){
    float d = 0.0;     // the current distance the ray has travelled
    float delta = 0.0; // the distance between the current position and the nearest surface
    for(int i = 0; i < MAX_ITERATIONS; i++){ // can't use while loops in WebGL shaders :'(
      delta = sDist(pixel + d*ray);
      if(delta <= EPSILON){ // we have a hit!
        surfaceHit = true;
        vec3 inter = pixel + d*ray;
        baseMaterial = sTexture(inter);
        vec3 refRay = reflect(normalize(inter - pixel), sNormal(inter));
        float t = intersectReflectionPlane(inter, refRay);
        if(t > 0.0){
          vec3 refProj = inter + t*refRay;
          vec2 refUV = vec2(refProj[2], -refProj[1]) / 60.0 + vec2(0.45, 0.1);
          refUV = refUV * 0.7;
          if(abs(refUV[0]) <= 1.0 && abs(refUV[1]) <= 1.0){
            vec3 reflectionKD = texture2D(u_image, refUV+vec2(0.5, 0.5) / 2.0).rgb;
            if(reflectionKD != vec3(1.0)){
              vec3 V = -ray;
              vec3 H = (V + refRay) / (length(V + refRay));
              float refWeight = max(0.0, 1.0 - abs(pow(dot(H, V)+0.14, 1.0)));//0.95;
              baseMaterial = reflectionKD * refWeight + baseMaterial * (1.0-refWeight);
            }
          }
        }
        return vec4(blinnPhongShade(pixel + d*ray, camPos), 1.0);
      }
      d = d + delta;
      if(d >= MAX_DISTANCE){
        break;
      }
    }
    // no hits, return white background
    return vec4(1.0, 1.0, 1.0, 0.0);
  }

  void main(void) {
    // Give this an initial value since we increment it in the loop
    gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);

    // sensible init vals...
    float minR = 1.0;
    float minG = 1.0;
    float minB = 1.0;
    float maxR = 0.0;
    float maxG = 0.0;
    float maxB = 0.0;

    float uoffset;
    float voffset;
    vec2 adjUV;
    vec3 nright;
    vec3 nup;
    vec3 dir;
    vec3 ray;

    for(int i = 0; i < RAYS_PER_PIXEL; i++){ // stochastic AA
      vec2 co = (float(i)+t) * uv;
      float r1 = snoise(co);
      float r2 = snoise(co * 2.240013);

      // the >1 factor potentially shoots rays outside of the pixel
      // this adds a little bit of noise to edges and helps reduce
      // aliasing artifacts while keeping the performance cost reasonable
      uoffset = 1.35*(r1-0.5)/IMAGE_WIDTH;
      voffset = 1.35*(r2-0.5)/IMAGE_WIDTH;
      adjUV = (uv+vec2(uoffset, voffset))*2.0 - vec2(1.0, 1.0);

      // camera ray + pixel calculations
      nright = normalize(cross(camUp, camDir));
      nup = normalize(cross(nright, camDir));
      dir = normalize(camDir);
      vec3 pixel = camPos + dir + nright*adjUV[0]*ulen + nup*adjUV[1]*vlen;
      vec3 ray = normalize(pixel - camPos);

      // FIRE THE MISSILES!
      vec4 res = traceRay(pixel, ray);

      // bookkeeping to detect potential edges for AA
      minR = min(minR, res[0]);
      minG = min(minG, res[1]);
      minB = min(minB, res[2]);
      maxR = max(maxR, res[0]);
      maxG = max(maxG, res[1]);
      maxB = max(maxB, res[2]);
      
      if(!surfaceHit && i >= MISS_THRESHOLD){
        gl_FragColor = vec4(float(RAYS_PER_PIXEL)); // white bg
        break;
      }
      
      if(i == THRESHOLD_TEST && ((maxR+maxG+maxB) - (minR+minG+minB)) <= COL_THRESHOLD){
        gl_FragColor = res * float(RAYS_PER_PIXEL);
        break;
      }
      
      gl_FragColor += res;
    }

    // average out the total AA rays and apply gamma correction
    gl_FragColor *= (1.0/float(RAYS_PER_PIXEL));
    gl_FragColor = pow(gl_FragColor, vec4(1.0/2.2));

    if(PERF_DEBUG){
      const float dcpc = 40.0; // distance calls per channel
      float r = min(max(0.0, (sDistCalls-2.0*dcpc)/(2.0*dcpc)), 1.0);
      float g = min(max(0.0, (sDistCalls-dcpc)/dcpc), 1.0);
      float b = min(sDistCalls/dcpc, 1.0) - g;
      g = g - r;
      gl_FragColor = vec4(r, g, b, 1.0);
    }
  }
</script>

<script id="shader-vs" type="x-shader/x-vertex">
  precision mediump float;
  varying vec2 uv;
  attribute vec3 aVertexPosition;
  uniform mat4 uMVMatrix;
  uniform mat4 uPMatrix;
  void main(void) {
    uv = vec2(aVertexPosition[0], 1.0 - aVertexPosition[1]);
    gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
  }
</script>

<script type="text/javascript" src="avatar.js"></script>
<script type="text/javascript" src="initGL.js"></script>
</head>

<body onload="webGLStart(); animateAvatar();">
  <h1 style="margin-bottom:0px;" class="fadein">fabrice_castel</h1>
  <canvas id="canvas" style="border: none; margin-left: auto; margin-right: auto; display: block; background: url(assets/glplaceholder.png)" width="400" height="400"></canvas>
  <ul style="margin-top:0px;" class="fadein">
    <li><a href="projects.html">$(projects)</a></li>
    <li><a href="blog.html">__blog__.cpp</a></li>
    <li>. . .</li>
    <li><a href="contact.html">contactInformation.getInTouch()</a></li>
  </ul>
  <div id="landingPageAvatarContainer">
    <img src="assets/avatar_idle.png" class="avatar" id="landingPageAvatar"/>
  </div>
</body>

</html>